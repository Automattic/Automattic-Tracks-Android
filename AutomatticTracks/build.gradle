plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

repositories {
    google()
    maven {
        url 'https://a8c-libs.s3.amazonaws.com/android'
        content {
            includeGroup "org.wordpress"
            includeGroup "org.wordpress.fluxc"
            includeGroup "org.wordpress.wellsql"
        }
    }
    maven {
        url "https://a8c-libs.s3.amazonaws.com/android/jcenter-mirror"
        content {
            includeVersion "com.android.volley", "volley", "1.1.1"
        }
    }
}

dependencies {
    implementation platform('io.sentry:sentry-bom:5.4.3')
    implementation "io.sentry:sentry-android"
    implementation "io.sentry:sentry-android-okhttp"
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.assertj:assertj-core:3.19.0'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:2.2.11'
    testImplementation 'org.mockito:mockito-inline:3.8.0'

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2"

    //FluxC
    implementation "org.wordpress:fluxc:1.36.0"

    //WordPress utils
    implementation "org.wordpress:utils:2.3.0"

    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.2"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:runner:1.3.0'
}

android {
    compileSdkVersion 30

    defaultConfig {
        versionName "2.1.0"
        minSdkVersion 21
        targetSdkVersion 30
        buildConfigField "String", "SENTRY_TEST_PROJECT_DSN", project.properties.get("sentryTestProjectDSN")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments('notClass': 'com.automattic.android.tracks.crashlogging.SendEventsToSentry')
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        baseline file("lint-baseline.xml")
        warningsAsErrors true
        warning 'GradleDependency'
    }

    sourceSets {
        androidTest {
            java.srcDirs += "src/sharedTest/kotlin"
        }
        test {
            java.srcDirs += "src/sharedTest/kotlin"
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                artifactId 'Automattic-Tracks-Android'
                description 'Android client for Nosara tracks (event tracking and analytics)'
                from components.release
                groupId 'com.automattic'
                pom {
                    licenses {
                        license {
                            name = 'GNU General Public License v2.0'
                            url = 'https://www.gnu.org/licenses/old-licenses/gpl-2.0.en.html'
                        }
                    }
                }
            }
        }
    }
}
