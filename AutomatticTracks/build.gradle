plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'com.automattic.android.publish-to-s3'
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    implementation platform("io.sentry:sentry-bom:6.12.1")
    implementation "io.sentry:sentry-android"
    implementation "io.sentry:sentry-android-okhttp"
    implementation "io.sentry:sentry-android-fragment"
    implementation "androidx.annotation:annotation:1.1.0"
    implementation "com.squareup.okhttp3:okhttp:4.9.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"

    testImplementation "junit:junit:4.13.2"
    testImplementation "org.assertj:assertj-core:3.19.0"
    testImplementation "org.mockito.kotlin:mockito-kotlin:4.0.0"
    testImplementation "org.mockito:mockito-inline:4.8.1"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4"

    androidTestImplementation "androidx.test.ext:junit:1.1.4"
    androidTestImplementation "androidx.test:runner:1.5.0"
}

android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        buildConfigField "String", "SENTRY_TEST_PROJECT_DSN", project.properties.get("sentryTestProjectDSN")
        buildConfigField "String", "VERSION_NAME", "\"2.1.0\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments('notClass': 'com.automattic.android.tracks.crashlogging.SendEventsToSentry')
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        baseline file("lint-baseline.xml")
        warningsAsErrors true
    }

    sourceSets {
        androidTest {
            java.srcDirs += "src/sharedTest/kotlin"
        }
        test {
            java.srcDirs += "src/sharedTest/kotlin"
        }
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                from components.release

                groupId 'com.automattic'
                artifactId 'Automattic-Tracks-Android'
                // version is set by 'publish-to-s3' plugin

                description 'Android client for Nosara tracks (event tracking and analytics)'
                pom {
                    licenses {
                        license {
                            name = 'GNU General Public License v2.0'
                            url = 'https://www.gnu.org/licenses/old-licenses/gpl-2.0.en.html'
                        }
                    }
                }
            }
        }
    }
}
