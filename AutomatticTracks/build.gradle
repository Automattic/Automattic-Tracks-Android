buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.2'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.31'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

repositories {
    google()
    jcenter()
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.pinterest:ktlint:0.40.0"
    implementation 'io.sentry:sentry-android:4.3.0'
    implementation 'androidx.annotation:annotation:1.1.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.assertj:assertj-core:3.19.0"

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:runner:1.3.0'
}

apply from: 'ktlint.gradle'

android {
    compileSdkVersion 30

    defaultConfig {
        versionName "1.2.1"
        minSdkVersion 16
        targetSdkVersion 30
        buildConfigField "String", "SENTRY_TEST_PROJECT_DSN", project.properties.get("sentryTestProjectDSN")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments('notClass': 'com.automattic.android.tracks.crashlogging.SendEventsToSentry')
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        baseline file("lint-baseline.xml")
        warningsAsErrors true
        warning 'GradleDependency'
    }

    sourceSets {
        androidTest {
            java.srcDirs += "src/sharedTest/kotlin"
        }
        test {
            java.srcDirs += "src/sharedTest/kotlin"
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                artifactId 'Automattic-Tracks-Android'
                description 'Android client for Nosara tracks (event tracking and analytics)'
                from components.release
                groupId 'com.automattic'
                pom {
                    licenses {
                        license {
                            name = 'GNU General Public License v2.0'
                            url = 'https://www.gnu.org/licenses/old-licenses/gpl-2.0.en.html'
                        }
                    }
                }
            }
        }
    }
}
