plugins {
    id "com.android.library"
    id "org.jetbrains.kotlin.android"
    id 'com.automattic.android.publish-to-s3'
}

repositories {
    google()
    maven {
        url "https://a8c-libs.s3.amazonaws.com/android"
        content {
            includeGroup "org.wordpress"
            includeGroup "org.wordpress.fluxc"
            includeGroup "org.wordpress.wellsql"
        }
    }
}

android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 21
        //noinspection OldTargetApi
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        warningsAsErrors true
        warning 'GradleDependency'
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

dependencies {
    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2"

    //FluxC
    implementation "org.wordpress:fluxc:1.36.0"

    //WordPress utils
    implementation "org.wordpress:utils:2.3.0"

    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.2"
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.assertj:assertj-core:3.19.0'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:2.2.11'
    testImplementation 'org.mockito:mockito-inline:3.8.0'
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                from components.release

                groupId 'com.automattic.tracks'
                artifactId 'experimentation'
                // version is set by 'publish-to-s3' plugin
            }
        }
    }
}

